<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_PositionStatePMPS2D" Id="{9dffd600-2d32-4e33-bb6b-8aab4cc29ee5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PositionStatePMPS2D
(*
    Proof of concept for a 2D pmps state mover
    This should illustrate how many more building blocks need to be added to reach the goal
    When finished, this should be usable as a function block in live PLCs, just pragma and add your enum
    Need to also make 1D, 3D, and non-pmps versions
    Known missing:
    - Some FFOs aren't here yet
    - Maintenance mode
    - Arbiter disable
    TODO: can I make it super easy to churn these out? Or are we doomed to a copy and paste?
*)
VAR_IN_OUT
    stMotionStage1: DUT_MotionStage;
    {attribute 'pytmc' := '
        pv: STATES:M1
        io: io
        expand: %.2d
    '}
    astPositionStates1: ARRAY[1..GeneralConstants.MAX_STATES] OF DUT_PositionState;
    stMotionStage2: DUT_MotionStage;
    {attribute 'pytmc' := '
        pv: STATES:M2
        io: io
        expand: %.2d
    '}
    astPositionStates2: ARRAY[1..GeneralConstants.MAX_STATES] OF DUT_PositionState;
    fbFFHWO: FB_HardwareFFOutput;
    fbArbiter: FB_Arbiter;
END_VAR
VAR_INPUT
    // A name to use as reference for fast faults, etc., defaults to motor1's name.
    sDeviceName: STRING;
    // The database lookup string for the transition state.
    sTransitionKey: STRING;
    // How long to wait for parameters before timing out
    tArbiterTimeout: TIME := T#1s;
    // Whether to fault and move on timeout (TRUE) or to wait (FALSE)
    bMoveOnArbiterTimeout: BOOL := TRUE;
    // Set this to TRUE when you need to reset the BPTM timeout FB, this surprisingly case specific
    bResetBPTMTimeout: BOOL;
    {attribute 'pytmc' := '
        pv: STATES
    '}
    stEpicsInput: ST_StateEpicsInput;
    {attribute 'pytmc' := '
        pv: STATES
    '}
    stPMPSEpicsInput: ST_StatePMPSEpicsInput;
END_VAR
VAR_OUTPUT
    {attribute 'pytmc' := '
        pv: STATES
    '}
    stEpicsOutput: ST_StateEpicsOutput;
END_VAR
VAR
    astAllMotors: ARRAY[1..2] OF DUT_MotionStage;
    astFullPositionStates: ARRAY[1..2, 1..GeneralConstants.MAX_STATES] OF DUT_PositionState;
    astDbStateParams: ARRAY[0..GeneralConstants.MAX_STATES] OF ST_DbStateParams;

    fbInternalND: FB_PositionStateInternalND;
    afbInternal: ARRAY[1..2, 1..GeneralConstants.MAX_STATES] OF FB_PositionStateInternal;
    fbDB: FB_MotionReadPMPSDBND;
    fbBPTM: FB_MotionBPTM;
    fbLimits: FB_StatePMPSLimitsND;
    afbStateLimits: ARRAY[1..2] OF FB_StatePMPSLimits;
    fbMove: FB_PositionStateMoveND;
    afbMove: ARRAY[1..2] OF FB_PositionStateMove;
    fbRead: FB_PositionStateReadND;
    afbRead: ARRAY[1..2] OF FB_PositionStateRead;

    nGoalIndex: DINT;
    nPrevGoalIndex: DINT;
    astGoalPositionStates: ARRAY[1..2] OF DUT_PositionState;
    bTimeToMove: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
// TODO if I assign like this here and invert at the end of the FB, does it work?
// Otherwise I need to use POINTER or REFERENCE
astAllMotors[1] := stMotionStage1;
astAllMotors[2] := stMotionStage2;

astFullPositionStates[1] := astPositionStates1;
astFullPositionStates[2] := astPositionStates2;

fbInternalND(
    astMotionStage:=astAllMotors,
    astPositionState:=astFullPositionStates,
    afbStateInternal:=afbInternal,
    bArrayMismatchError=>,
);

fbDB(
    astPositionState:=astFullPositionStates,
    astDbStateParams:=astDbStateParams,
    fbFFHWO:=fbFFHWO,
    sTransitionKey:=sTransitionKey,
    sDeviceName:=sDeviceName,
    bError=>,
    bArrayMismatchError=>,
);

// Sanitize the input in case we get something wild from EPICS
IF 1 <= stEpicsInput.nSetValue AND stEpicsInput.nSetValue <= GeneralConstants.MAX_STATES THEN
    nGoalIndex := stEpicsInput.nSetValue;
ELSE
    // Transition state should always be the safest
    nGoalIndex := 0;
END_IF

fbBPTM(
    astMotionStage:=astAllMotors,
    fbArbiter:=fbArbiter,
    fbFFHWO:=fbFFHWO,
    stGoalParams:=astDbStateParams[nGoalIndex].stBeamParams,
    stTransParams:=astDbStateParams[0].stBeamParams,
    bAtState:=fbRead.bKnownState AND fbRead.nPositionIndex=nGoalIndex,
    sDeviceName:=sDeviceName,
    tArbiterTimeout:=tArbiterTimeout,
    bMoveOnArbiterTimeout:=bMoveOnArbiterTimeout,
    bResetBPTMTimeout:=bResetBPTMTimeout,
    bTransitionAuthorized=>,
    bDone=>,
);

fbLimits(
    astMotionStage:=astAllMotors,
    astPositionState:=astFullPositionStates,
    afbStateLimits:=afbStateLimits,
    fbFFHWO:=fbFFHWO,
    nGoalStateIndex:=nGoalIndex,
    bArrayMismatchError=>,
);

IF nGoalIndex <> 0 THEN
    astGoalPositionStates[1] := astFullPositionStates[1, nGoalIndex];
    astGoalPositionStates[2] := astFullPositionStates[2, nGoalIndex];
END_IF
bTimeToMove S= nGoalIndex <> nPrevGoalIndex;
nPrevGoalIndex := nGoalIndex;

fbMove(
    astMotionStage:=astAllMotors,
    astPositionState:=astGoalPositionStates,
    afbPositionStateMove:=afbMove,
    bExecute:=bTimeToMove,
    bReset:=stEpicsInput.bReset,
    enumMotionRequest:=ENUM_MotionRequest.INTERRUPT,
    bAtState=>,
    bBusy=>,
    bDone=>,
    bError=>,
    nErrorCount=>,
    nShownError=>,
    nErrorID=>,
    sErrorMessage=>,
);
bTimeToMove R= fbMove.bDone;

fbRead(
    astMotionStage:=astAllMotors,
    astPositionState:=astFullPositionStates,
    afbPositionStateRead:=afbRead,
    bKnownState=>,
    bMovingState=>,
    nPositionIndex=>stEpicsOutput.nGetValue,
    bArrayMismatchError=>,
);

// Need to update the global state of the motion DUTs
// See top of this FB
stMotionStage1 := astAllMotors[1];
stMotionStage2 := astAllMotors[2];

astPositionStates1 := astFullPositionStates[1];
astPositionStates2 := astFullPositionStates[2];
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>