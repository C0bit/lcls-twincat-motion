<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_PositionStatePMPS" Id="{7e0e3094-8a3d-4636-a390-eb10e10412f0}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PositionStatePMPS EXTENDS FB_PositionStatePMPS_Base
(*
    Hooks up a position state to an arbiter and an FFO
        Use BeamParameterTransitionManager to manage transition requests between states
        Hook up to the inputs/outputs of the state function block
        Raises FFO if beam parameters are worse than required for current state
*)
VAR_IN_OUT
    fbArbiter: FB_Arbiter;
    fbFFHWO: FB_HardwareFFOutput;
END_VAR
VAR
    bptm: BeamParameterTransitionManager;
    ffBeamParamsOk: FB_FastFault;
    bFFOxOk: BOOL;
    bAtSafeState: BOOL;
    nIter: INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Exec();]]></ST>
    </Implementation>
    <Action Name="ClearAsserts" Id="{a5b537df-5b20-41f7-b1d6-59ce223e5b6e}">
      <Implementation>
        <ST><![CDATA[fbArbiter.RemoveRequest(nTransitionAssertionID);
fbArbiter.RemoveRequest(nUnknownAssertionID);
FOR nIter := 1 TO 15 DO
    fbArbiter.RemoveRequest(arrStates[nIter].nRequestAssertionID);
END_FOR]]></ST>
      </Implementation>
    </Action>
    <Action Name="HandleBPTM" Id="{15e95ede-ce74-498f-962a-8707e856078c}">
      <Implementation>
        <ST><![CDATA[bptm(fbArbiter:=fbArbiter,
     i_TransitionAssertionID:=nTransitionAssertionID,
     i_stTransitionAssertion:=stTransitionBeam,
     i_nRequestedAssertionID:=stStateReq.nRequestAssertionID,
     i_stRequestedAssertion:=stStateReq.stBeamParams,
     i_xMoving:=bDoingTransition,
     i_xDoneMoving:=NOT bDoingTransition,
     stCurrentBeamParameters:=PMPS_GVL.stCurrentBeamParameters,
     q_xTransitionAuthorized=>bTransitionAuthorized);]]></ST>
      </Implementation>
    </Action>
    <Action Name="HandleFFO" Id="{6b3afb71-b9c2-4718-aacf-85bf75e65e9c}">
      <Implementation>
        <ST><![CDATA[// If the beam parameters are wrong, it is a fault! This encompasses all arbiter-related errors.
// Get state will point to Unknown/No beam if we are out of state bounds or in motion
// Otherwise we'll have the current state's beam parameters

IF stBeamNeeded.nRate <= 0 THEN
    // If nRate is zero, trip the beam just to be sure.
    bFFOxOk := FALSE;
ELSIF bArbiterTimeout AND bMoveOnArbiterTimeout THEN
    // If the arbiter times out, trip so we can get moving.
    bFFOxOk := FALSE;
ELSE
    // Otherwise, trip if the beam conditions are bad
    bFFOxOk := F_SafeBPCompare(PMPS_GVL.stCurrentBeamParameters, stBeamNeeded);
END_IF

// It is safe to reset automatically if our current state can take full beam.
// Otherwise we'll have to ask for a user acknowledgement to clear.
bAtSafeState := F_SafeBPCompare(PMPS_GVL.cstFullBeam, stBeamNeeded);

ffBeamParamsOk(
    i_xOK:=bFFOxOk,
    i_xReset:=bAtSafeState,
    i_DevName:=stMotionStage.sName,
    i_Desc:='Beam parameter mismatch',
    i_TypeCode:=1,
    io_fbFFHWO:=fbFFHWO);]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>