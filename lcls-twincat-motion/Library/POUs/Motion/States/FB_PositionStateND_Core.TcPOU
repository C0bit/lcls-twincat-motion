<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_PositionStateND_Core" Id="{be627b84-a239-4e6f-8f4f-59498d92de82}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PositionStateND_Core
(*
    Collection of all the actions shared between all states FBs
    This is used in e.g.
    - FB_PositionState1D
    - FB_PositionState2D
    - ... etc.
    - FB_PositionStatePMPS1D
    - FB_PositionStatePMPS2D
    - ... etc.
*)
VAR_IN_OUT
    astMotionStageMax: ARRAY[1..MotionConstants.MAX_STATE_MOTORS] OF ST_MotionStage;
    astPositionStateMax: ARRAY[1..MotionConstants.MAX_STATE_MOTORS] OF ARRAY[1..GeneralConstants.MAX_STATES] OF ST_PositionState;
    stEpicsInput: ST_StateEpicsInput;
    stEpicsOutput: ST_StateEpicsOutput;
    eEnumInput: INT;
    eEnumOutput: INT;
END_VAR
VAR_INPUT
    bEnable: BOOL;
    nActiveMotorCount: UINT;
END_VAR
VAR_OUTPUT
    nCurrGoal: UINT;
END_VAR
VAR
    fbInput: FB_StatesInputHandler;
    fbInternal: FB_PositionStateInternalND;
    fbMove: FB_PositionStateMoveND;
    fbRead: FB_PositionStateReadND;
    astMoveGoals: ARRAY[1..MotionConstants.MAX_STATE_MOTORS] OF ST_PositionState;
    stInvalidPos: ST_PositionState;
    nIterMotor: DINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
stEpicsInput.nSetValue := INT_TO_UINT(eEnumInput);

fbRead(
    astMotionStage:=astMotionStageMax,
    astPositionState:=astPositionStateMax,
    nActiveMotorCount:=nActiveMotorCount,
    bKnownState=>,
    bMovingState=>,
    nPositionIndex=>stEpicsOutput.nGetValue,
);

fbInput(
    stUserInput:=stEpicsInput,
    bMoveBusy:=fbMove.bBusy,
    nInitGoal:=fbRead.nPositionIndex,
    nCurrGoal=>nCurrGoal,
    bExecMove=>,
    bResetMove=>,
);

fbInternal(
    astMotionStage:=astMotionStageMax,
    astPositionState:=astPositionStateMax,
);

FOR nIterMotor := 1 TO MotionConstants.MAX_STATE_MOTORS DO
    IF nCurrGoal > 0 THEN
        astMoveGoals[nIterMotor] := astPositionStateMax[nIterMotor][nCurrGoal];
    ELSE
        astMoveGoals[nIterMotor] := stInvalidPos;
    END_IF
END_FOR

fbMove(
    astMotionStage:=astMotionStageMax,
    astPositionState:=astMoveGoals,
    nActiveMotorCount:=nActiveMotorCount,
    bExecute:=fbInput.bExecMove,
    bReset:=fbInput.bResetMove,
    enumMotionRequest:=ENUM_MotionRequest.INTERRUPT,
    bAtState=>,
    bError=>stEpicsOutput.bError,
    nErrorID=>stEpicsOutput.nErrorID,
    sErrorMessage=>stEpicsOutput.sErrorMsg,
    bBusy=>stEpicsOutput.bBusy,
    bDone=>stEpicsOutput.bDone,
);

eEnumInput := UINT_TO_INT(stEpicsInput.nSetValue);
eEnumOutput := UINT_TO_INT(stEpicsOutput.nGetValue);
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>